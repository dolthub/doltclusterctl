load("@io_bazel_rules_go//go:def.bzl", "TOOLS_NOGO", "go_binary", "go_library", "go_test", "nogo")
load("@bazel_gazelle//:def.bzl", "gazelle")
load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@com_github_bazelbuild_buildtools//buildifier:def.bzl", "buildifier")

buildifier(
    name = "buildifier",
)

# gazelle:prefix github.com/dolthub/doltclusterctl
# gazelle:exclude proto
# gazelle:proto disable_global
# gazelle:go_naming_convention import
gazelle(name = "gazelle")

gazelle(
    name = "gazelle-update-repos",
    args = [
        "-from_file=go.mod",
        "-to_macro=deps.bzl%go_dependencies",
        "-prune",
        "-build_file_proto_mode",
        "disable_global",
    ],
    command = "update-repos",
)

nogo(
    name = "nogo",
    config = "nogo_config.json",
    visibility = ["//visibility:public"],
    deps = TOOLS_NOGO + [],
)

go_library(
    name = "doltclusterctl_lib",
    srcs = [
        "config.go",
        "main.go",
    ],
    importpath = "github.com/dolthub/doltclusterctl",
    visibility = ["//visibility:private"],
    deps = [
        "@com_github_go_sql_driver_mysql//:mysql",
        "@io_k8s_api//apps/v1:apps",
        "@io_k8s_api//core/v1:core",
        "@io_k8s_apimachinery//pkg/apis/meta/v1:meta",
        "@io_k8s_apimachinery//pkg/fields",
        "@io_k8s_apimachinery//pkg/watch",
        "@io_k8s_client_go//kubernetes",
        "@io_k8s_client_go//rest",
    ],
)

go_binary(
    name = "doltclusterctl",
    embed = [":doltclusterctl_lib"],
    visibility = ["//visibility:public"],
)

container_image(
    name = "base",
    base = "@ubuntu2004//image",
    symlinks = {
        "/usr/local/bin/doltclusterctl": "/app/image.binary",
    },
    visibility = ["//visibility:private"],
)

go_image(
    name = "image",
    base = ":base",
    embed = [
        ":doltclusterctl_lib",
    ],
    goarch = "amd64",
    goos = "linux",
    visibility = ["//visibility:public"],
)

go_test(
    name = "doltclusterctl_test",
    size = "small",
    srcs = [
        "config_test.go",
        "main_test.go",
    ],
    data = glob(["testdata/**"]),
    embed = [":doltclusterctl_lib"],
    deps = ["@com_github_stretchr_testify//assert"],
)

test_suite(
    name = "coverage_tests",
    tests = [
        ":doltclusterctl_test",
    ],
)
