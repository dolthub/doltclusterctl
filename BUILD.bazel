load("@io_bazel_rules_go//go:def.bzl", "TOOLS_NOGO", "go_binary", "go_library", "go_test", "nogo")
load("@bazel_gazelle//:def.bzl", "gazelle")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@com_github_bazelbuild_buildtools//buildifier:def.bzl", "buildifier")

buildifier(
    name = "buildifier",
)

# gazelle:prefix github.com/dolthub/doltclusterctl
# gazelle:exclude proto
# gazelle:proto disable_global
# gazelle:go_naming_convention import
gazelle(name = "gazelle")

gazelle(
    name = "gazelle-update-repos",
    args = [
        "-from_file=go.mod",
        "-to_macro=deps.bzl%go_dependencies",
        "-prune",
        "-build_file_proto_mode",
        "disable_global",
    ],
    command = "update-repos",
)

nogo(
    name = "nogo",
    config = "nogo_config.json",
    visibility = ["//visibility:public"],
    deps = TOOLS_NOGO + [],
)

go_library(
    name = "doltclusterctl_lib",
    srcs = [
        "cluster.go",
        "commands.go",
        "config.go",
        "db.go",
        "kubernetes.go",
        "main.go",
    ],
    importpath = "github.com/dolthub/doltclusterctl",
    visibility = ["//visibility:private"],
    deps = [
        "@com_github_go_sql_driver_mysql//:mysql",
        "@io_k8s_api//apps/v1:apps",
        "@io_k8s_api//core/v1:core",
        "@io_k8s_apimachinery//pkg/apis/meta/v1:meta",
        "@io_k8s_apimachinery//pkg/fields",
        "@io_k8s_apimachinery//pkg/watch",
        "@io_k8s_client_go//kubernetes",
        "@io_k8s_client_go//rest",
    ],
)

go_binary(
    name = "doltclusterctl",
    embed = [":doltclusterctl_lib"],
    visibility = ["//visibility:public"],
)

go_binary(
    name = "doltclusterctl_linux_amd64",
    embed = [":doltclusterctl_lib"],
    goarch = "amd64",
    goos = "linux",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "app_layer",
    srcs = [":doltclusterctl_linux_amd64"],
    include_runfiles = True,
    package_dir = "app",
)

pkg_tar(
    name = "image_layer",
    symlinks = {
        "/usr/local/bin/doltclusterctl": "/app/doltclusterctl_linux_amd64",
    },
    deps = [":app_layer"],
)

oci_image(
    name = "image",
    base = "@ubuntu2004",
    tars = [
        ":image_layer",
    ],
    visibility = ["//visibility:public"],
)

oci_tarball(
    name = "image.tar",
    image = ":image",
    repo_tags = ["doltclusterctl:latest"],
    visibility = ["//visibility:public"],
)

go_test(
    name = "doltclusterctl_test",
    size = "small",
    srcs = [
        "commands_test.go",
        "config_test.go",
        "main_test.go",
    ],
    data = glob(["testdata/**"]),
    embed = [":doltclusterctl_lib"],
    deps = ["@com_github_stretchr_testify//assert"],
)

test_suite(
    name = "coverage_tests",
    tests = [
        ":doltclusterctl_test",
    ],
)
